// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // Increment value in register via feedback wire
    Inc16(in=feedback, out=incremented);

    // Go backwards in precedence to get correct value.
    // No increment flag is tested since it has lowest 
    // precedence. It is tested below
    Mux16(a=incremented, b=in, sel=load, out=v1);
    Mux16(a=v1, b=false, sel=reset, out=v2);

    // Use Or gates to check for any flags.
    // This is where the increment flag is picked up
    Or(a=reset, b=load, out=w1);
    Or(a=w1, b=inc, out=w2);

    // Finally, if flag, load propogated value
    Register(in=v2, load=w2, out=feedback, out=out);
}
